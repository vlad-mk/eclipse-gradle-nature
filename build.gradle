buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
	}
}


plugins {
	id 'groovy'
	id 'eclipse'
	id 'java-gradle-plugin'
	id 'maven-publish'
 }


apply plugin: "com.gradle.plugin-publish"

sourceCompatibility = 1.8
group = 'net.vlad'
version = '0.2.1'



jar {
    manifest {
        attributes 'EclipseGradleNaturePlugin': 'Gradle Nature Plugin for eclipse', 'Implementation-Version': version
    }
}

sourceSets
{
	main {
		groovy.srcDir 'src/main/groovy'
	}
	test {
		groovy.srcDir 'src/test/groovy'
	}
}


repositories {
    mavenCentral()
}

project.setBuildDir 'target/build'
eclipse.classpath.defaultOutputDir = file 'target/build-eclipse'


dependencies {
	compile gradleApi()
	compile localGroovy()
	
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

task resolve {
	//testCompile includes all other dependencies
	description 'Resolve all configurations'
	group 'Other'
	doLast {
		println '-' * 60
		println version
		println '-' * 60
		configurations.testCompile.sort().each { File file -> println file.name }
		println '-' * 60
	}
}

pluginBundle {
	website = 'https://github.com/vlad-mk/eclipse-gradle-nature'
	vcsUrl = 'https://github.com/vlad-mk/eclipse-gradle-nature'
	description = 'Addon for ecplise plugin, adds Buildship nature while generating the eclipse project'
	tags = ['release-0.2.1']

	plugins {
		eclipseGradleNature {
			id = 'net.vlad.eclipse-gradle-nature'
			displayName = 'Gradle(Buildship) nature plugin for ecplise'
		}
	}
}

