buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		jcenter()
	}
	dependencies {
		classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
	}
}


plugins {
	id 'groovy'
	id 'eclipse'
	id 'java-gradle-plugin'
	id 'maven-publish'
	id "com.gradle.plugin-publish" version "0.9.7"
	id "com.jfrog.bintray" version "1.7"
 }


//apply plugin: "com.gradle.plugin-publish"

sourceCompatibility = 1.8
group = 'net.vlad'
version = '0.2.2'

ext {
	githubUrl = 'https://github.com/vlad-mk/eclipse-gradle-nature'
}


jar {
    manifest {
        attributes 'EclipseGradleNaturePlugin': 'Gradle Nature Plugin for eclipse', 'Implementation-Version': version
    }
}

sourceSets
{
	main {
		groovy.srcDir 'src/main/groovy'
	}
	test {
		groovy.srcDir 'src/test/groovy'
	}
}


repositories {
    mavenCentral()
}

project.setBuildDir 'target/build'
eclipse.classpath.defaultOutputDir = file 'target/build-eclipse'


dependencies {
	compile gradleApi()
	compile localGroovy()
	
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

task resolve {
	//testCompile includes all other dependencies
	description 'Resolve all configurations'
	group 'Other'
	doLast {
		println '-' * 60
		println version
		println '-' * 60
		configurations.testCompile.sort().each { File file -> println file.name }
		println '-' * 60
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    include '**/*.java'
    include '**/*.groovy'
}

task javadocJar(type: Jar, dependsOn: [javadoc, groovydoc]) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    from groovydoc.destinationDir
}


artifacts {
    archives sourcesJar
    archives javadocJar
}

pluginBundle {
	website = githubUrl
	vcsUrl =  githubUrl
	description = 'Addon for ecplise plugin, adds Buildship nature while generating the eclipse project'
	tags = ["release-${project.version}"]

	plugins {
		eclipseGradleNature {
			id = "${group}.${project.name}" //'net.vlad.eclipse-gradle-nature'
			displayName = 'Gradle(Buildship) nature plugin for ecplise'
		}
	}
}

def pomConfig = {
	description 'Gradle eclipse plugin addon, to add buildship nature to eclipse project'
	name	 	project.name
	url 		githubUrl

	scm {
		url githubUrl
		connection "scm:git:${githubUrl}"
	}
	licenses {
		license {
			name 'The Apache Software License, Version 2.0'
			url 'http://www.apache.org/license/LICENSE-2.0.txt'
		}
	}
	developers {
		developer {
			id 'vlad-mk'
			name 'Vladislav Mikerin'
//			email 'your@mail.address'
		}
	}
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
			artifact 	sourcesJar
			artifact 	javadocJar
//			artifact 	groovydocJar
            groupId 	project.group
            artifactId 	project.name
            version 	project.version
			pom.withXml {
				asNode().children().last() + pomConfig
			}
        }
    }
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : 'xxxxx'
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : 'xxxxx'
	configurations = ['archives']
	publications = ['plugin']
	pkg {
		repo 	 = 'gradle-plugins'
		name 	 = project.name
		desc 	 = 'Gradle eclipse plugin addon, to add buildship nature to eclipse project'
		userOrg  = 'vlad-mk'
		licenses = ['Apache-2.0']
		vcsUrl 	 = githubUrl
		labels	 = ['gradle', 'plugin', 'eclips']
		publicDownloadNumbers = true
		version {
			name = "${project.version}"
//			desc = 'Gradle Bintray Plugin 1.0 final'
			vcsTag = "release-${project.version}"
//			attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
		}

	}
	dryRun = false
}


bintrayUpload {
	dependsOn "build", "generatePomFileForPluginPublication"
}